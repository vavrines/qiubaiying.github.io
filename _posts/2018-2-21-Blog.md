---
layout:     post
title:      Cholesky分解的程序实现
subtitle:   基于Fortran90 
date:       2018-2-21
author:     Vavrines
header-img: img/flower.jpg
catalog: true
tags:
- Research
---

## Intro

在实现基于Ellipsoidal Statistical Fokker-Planck模型的粒子算法时，一个关键的步骤是寻找满足下面关系的矩阵L：  
![](https://wikimedia.org/api/rest_v1/media/math/render/svg/a498ef3255cff22b477793f47cd18b5e80b7418a)  
对于这个问题，最直观的方法显然是对矩阵A开方，这需要计算A的特征值，在推进算法中会在每一个时间步耗费不少的计算时间。  

一个可行的替代方法是Cholesky分解，其原理是将一个正定的Hermite矩阵分解成一个下三角矩阵与其共轭转置之乘积。这种分解方式最初由André-Louis Cholesky发明。
    
## 算法原理

Cholesky分解的实现可以基于Cholesky-Crout算法。  
对于需要求解的矩阵，可以将其写成以下的形式：  
![](https://wikimedia.org/api/rest_v1/media/math/render/svg/15da0fc67622d164be459628c34b9fb6bae59bd3)  
相应的矩阵L元素的计算公式如下：  
![](https://wikimedia.org/api/rest_v1/media/math/render/svg/3c66080a03434674c342609693f813f946e98eb9)  

## 程序实现

下面给出了一个基于Fortran90的子程序。  
详细的版本发在了[Github](https://github.com/vavrines/cholesky "Title").

<pre>

!-----------------------------------------------------------------------------
! cholesky.f90
! Calculate Cholesky decomposition based on Cholesky-Crout algorithm
! Author: Tianbai Xiao
! Licence: GPLv3
! > A is a n by n positive definite real symmetric matrix
! > A = L L^T, where L is a lower triangular matrix and L^T is transpose of L.
!-----------------------------------------------------------------------------

subroutine calc_cholesky(n, A, L)

implicit none
integer, intent(in) :: n
real(kind=8), intent(in) :: A(n,n)
real(kind=8), intent(out) :: L(n,n)
integer :: i,j

! check of positive definite
do i = 1, n
if (A(i,i).le.0.0d0) then
return
end if
end do

! Cholesky-Crout algorithm
L(:,:) = 0.d0
do j = 1, n
L(j,j) = sqrt( A(j,j) - dot_product(L(j,1:j-1),L(j,1:j-1)) )
do i = j+1, n
L(i,j)  = ( A(i,j) - dot_product(L(i,1:j-1),L(j,1:j-1)) ) / L(j,j)
end do
end do

end subroutine calc_cholesky

</pre>    
